plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

def libVersionName = "1.0.0"

android {
    compileSdk 29

    defaultConfig {
        minSdk 23
        targetSdk 29
        versionCode 1
        versionName "$libVersionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "net.imoya.android.voiceclock.player"
            artifactId "imoya-android-voiceclock-player"
            version "$libVersionName"
            artifact sourceJar
            artifact "$buildDir/outputs/aar/lib-release.aar"

            // include any transitive dependencies
            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        if (it.group != null || it.name != null || it.version != null || it.name == "unspecified") return

                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            /** Configure path of your package repository on Github
             ** Replace GITHUB_USERID with your/organisation Github userID
             ** and REPOSITORY with the repository name on GitHub
             */
            url = uri("https://maven.pkg.github.com/IceImo-P/VoiceClockPlayerLib")
            credentials {
                /** Create github.properties in root project folder file with
                 ** gpr.user=GITHUB_USER_ID & gpr.token=PERSONAL_ACCESS_TOKEN
                 ** Set env variable GPR_USER & GPR_TOKEN if not adding a properties file**/

                username = githubProperties['gpr.user'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.token'] ?: System.getenv("GPR_TOKEN")
            }
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.6.0"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'net.imoya.android.util:imoya-android-util:1.4.0'
    implementation 'net.imoya.android.media:imoya-android-media:1.2.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}